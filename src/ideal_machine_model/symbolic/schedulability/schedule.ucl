/**
 * Demonstrate a general schedulability analysis framework.
 */

module A {
    output finish : integer;
    const exec_time : integer = 0;
    const ddl : integer = 5; // assume the current timestamp is 0.
    var ddl_high_priority : boolean;

    init {
        finish = -1;
        ddl_high_priority = false;
    }

    next {
        finish' = 0 + exec_time;
    }
}

module B {
    input start : integer;
    output finish : integer;
    const exec_time : integer = 3;
    const ddl : integer = 5; // assume the current timestamp is 0.
    var ddl_high_priority : boolean;

    init {
        finish = -1;
        ddl_high_priority = false;
    }

    next {
        finish' = start + exec_time;
    }
}

module C {
    input start : integer;
    output finish : integer;
    const exec_time : integer = 1; // 3 + 1 < 5
    const ddl : integer = 5; // assume the current timestamp is 0.
    var ddl_violation : boolean;
    var ddl_high_priority : boolean;

    define ddl_violated(t : integer) : boolean
    = if (t > ddl)
        then true else false;

    init {
        ddl_violation = false;
        finish = -1;
        ddl_high_priority = false;
    }

    next {
        ddl_violation' = ddl_violated(start);
        finish' = start + exec_time;
    }
}

module D {
    input start : integer;
    output finish : integer;
    const exec_time : integer = 3;
    const ddl : integer = 5; // assume the current timestamp is 0.
    var ddl_high_priority : boolean;

    init {
        finish = -1;
        ddl_high_priority = false;
    }

    next {
        finish' = start + exec_time;
    }
}

module E {
    input start : integer;
    output finish : integer;
    const exec_time : integer = 1; // 3 + 1 < 5
    const ddl : integer = 5; // assume the current timestamp is 0.
    var ddl_violation : boolean;
    var ddl_high_priority : boolean;

    define ddl_violated(t : integer) : boolean
    = if (t > ddl)
        then true else false;

    init {
        ddl_violation = false;
        finish = -1;
        ddl_high_priority = true;
    }

    next {
        ddl_violation' = ddl_violated(start);
        finish' = start + exec_time;
    }
}

module main {
    type reactor_t = enum { RB, RC, RD, RE };
    type schedule_t = { reactor_t,
                         reactor_t, 
                         reactor_t, 
                         reactor_t };

    var current_physical_time : integer;
    var step : integer;
    var running : reactor_t;
    var schedule : schedule_t;
    var importanceSum : integer;

    // Importances
    const b_importance : integer = 1;
    const c_importance : integer = 1;
    const d_importance : integer = 1;
    const e_importance : integer = 1;


    define tupleGet(s : schedule_t, i : integer) : reactor_t
    = if (i == 1) then s._1 else
        (if (i == 2) then s._2 else
            (if (i == 3) then s._3 else
                (if (i == 4) then s._4 else s._4)));

    define getImp(r : reactor_t) : integer
    = if (r == RB) then b_importance else
        (if (r == RC) then c_importance else
            (if (r == RD) then d_importance else
                (if (r == RE) then e_importance else 0)));

    define discountedImportance(s : schedule_t) : integer
    = (4 * getImp(s._1)) + (3 * getImp(s._2)) + (2 * getImp(s._3)) + (1 * getImp(s._4)); 

    /** 
     * Should calculate the precedence relationship 
     * from levels and chain id
     */
    // Starting reactor can only be B or D
    assume(schedule._1 == RB || schedule._1 == RD);

    // B precedes C
    assume(!(exists (i : integer, j : integer) ::
            i >= 1 && i <= 4 &&
            j >= 1 && j <= 4 &&
            tupleGet(schedule, i) == RB &&
            tupleGet(schedule, j) == RC &&
            i > j));

    // D precedes E 
    assume(!(exists (i : integer, j : integer) ::
            i >= 1 && i <= 4 &&
            j >= 1 && j <= 4 &&
            tupleGet(schedule, i) == RD &&
            tupleGet(schedule, j) == RE &&
            i > j));

    // Cannot schedule the same reactor twice.
    assume(!(exists (i : integer, j : integer) ::
            i >= 1 && i <= 4 &&
            j >= 1 && j <= 4 &&
            i != j &&
            tupleGet(schedule, i) == tupleGet(schedule, j)));

    // instance a : A(finish : (current_physical_time));
    instance b : B(start : (current_physical_time), finish : (current_physical_time));
    instance c : C(start : (current_physical_time), finish : (current_physical_time));
    instance d : D(start : (current_physical_time), finish : (current_physical_time));
    instance e : E(start : (current_physical_time), finish : (current_physical_time));

    init {
        step = 0;
        current_physical_time = 0;
        importanceSum = discountedImportance(schedule);
    }

    next {
        if (step <= 3) {
            case 
                (step == 0) : {
                    running' = schedule._1;
                }
                (step == 1) : {
                    running' = schedule._2;
                }
                (step == 2) : {
                    running' = schedule._3;
                }
                (step == 3) : {
                    running' = schedule._4;
                }
            esac

            case
                (running' == RB) : {
                    next(b);
                }
                (running' == RC) : {
                    next(c);
                }
                (running' == RD) : {
                    next(d);
                }
                (running' == RE) : {
                    next(e);
                }
            esac

            step' = step + 1;
        }
    }

    property[LTL] no_c_ddl_violation : !F(
                                            step == 4 
                                            // High-priority deadline(s)
                                            && e.ddl_violation == false
                                            // && c.ddl_violation == false
                                            && (forall (s : schedule_t) :: 
                                                discountedImportance(schedule) >=
                                                discountedImportance(s))
                                        );

    control {
        v = bmc(5);
        check;
        print_results;
        v.print_cex(
                    current_physical_time,
                    schedule,
                    step,
                    running,
                    importanceSum,
                    b.start,
                    b.finish,
                    c.start,
                    c.finish,
                    c.ddl_violation,
                    d.start,
                    d.finish,
                    e.start,
                    e.finish,
                    e.ddl_violation
                    );
    }
}
