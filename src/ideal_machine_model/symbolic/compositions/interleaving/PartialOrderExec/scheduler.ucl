/**
 * Coordinates event passing between reactors and priority queues
 * and adjust the local clock.
 */
// File specific
module Finalized {
    type final_tb_t = { 
                        boolean,
                        boolean,
                        boolean 
                      };
}

module Scheduler {
    define * = Common.*; // Import is_present()
    type * = EventQ.*;
    const * = EventQ.*;
    type * = Finalized.*;

    // File specific: comm. channels between scheduler and reactors
    input Source_to_S : event_t;
    input A_to_S : event_t;
    input B_to_S : event_t;

    output S_to_Source : event_t;
    output S_to_A : event_t;
    output S_to_B : event_t;

    define get(q : final_tb_t, i : integer) : boolean
    = if (i == 1) then q._1 else
        (if (i == 2) then q._2 else
            (if (i == 3) then q._3 else
                false));

    define set(q : final_tb_t, i : integer, v : boolean) : final_tb_t
    = if (i == 1) then {v, q._2, q._3} else
        (if (i == 2) then {q._1, v, q._3} else
            (if (i == 3) then {q._1, q._2, v} else
                 q));

    // Finalized REACTION table
    // var finalized : [trigger_id_t]boolean;
    var finalized : final_tb_t;
    var f_counter : integer;
    input finalize_Source_startup : boolean;
    input finalize_A_in : boolean;
    input finalize_B_in : boolean;
    output final_to_Source : final_tb_t;
    output final_to_A : final_tb_t;
    output final_to_B : final_tb_t;

    /**
     * Notes:
     * These two queues can potentially be merged
     * and produce an _out_reaction array.
     * Similarly, A and B are merged into one module
     * containing all reactions and state variables.
     * This allows all declarations to be in one local scope.
     * But need to check whether this is actually useful,
     * since we can do the same things in main module.
     *
     * The same _out_reaction can be fed to both A and B,
     * where they selectively react to triggers with
     * them as the destinations.
     */

    // The "null" event
    var NULL_EVENT : event_t;

    // Event queue variables 
    // Both queues share these two fields for now.
    // If need be, separate them and create a NoOp operation for queues.
    var eq_op : op_t;
    var eq_data : data_t;
    var eq_out : data_t;

    // Reaction queue variables
    var rq_op : op_t;
    var rq_data : data_t;
    var rq_out : data_t;

    // Event queue and reaction queue
    instance event_q : EventQ(op : (eq_op), data : (eq_data), out : (eq_out));
    instance reaction_q : ReactionQ(op : (rq_op), data : (rq_data), out : (rq_out));

    // The current clock value
    var t : instant_t;

    // File specific: helper predicates
    define has_available_event() : boolean
    = (is_present(Source_to_S)
        || is_present(A_to_S)
        || is_present(B_to_S));

    // TODO: change the logic when using the updated event_t
    // w/ an explicit reaction/action indicator.
    define is_reaction(e : event_t) : boolean
    = if (e._1 == t) then true else false; 

    define get_event_present(e1 : event_t, e2 : event_t) : event_t
    = if (is_present(e1)) then e1 else e2;

    define get_event_dest(e : event_t, dest : reactor_id_t) : event_t
    = if (e._3 == dest) then e else NULL_EVENT;

    /**
     * Load a non-empty input to push.
     */
    procedure push_event(e : event_t)
        returns (
            eq_op : op_t,
            eq_data : data_t,
            rq_op : op_t,
            rq_data : data_t
        )
    {
        if (is_reaction(e)) {
            rq_op = PUSH;
            rq_data = e;

            // NoOp
            eq_op = PUSH;
            eq_data = NULL_EVENT;
        }
        else {
            eq_op = PUSH;
            eq_data = e;

            // NoOp
            rq_op = PUSH;
            rq_data = NULL_EVENT;
        }
    }

    procedure update_finalized_table()
        modifies finalized;
    {
        if (finalize_Source_startup) {
            finalized = set(finalized, 1, true);
        }
        if (finalize_A_in) {
            finalized = set(finalized, 2, true);
        }
        if (finalize_B_in) {
            finalized = set(finalized, 3, true);
        }
    }

    init {
        NULL_EVENT = { -1, NULL, NULL, NULL_NULL, -1, false };
        t = 0;

        eq_op = PUSH;
        eq_data = NULL_EVENT;

        rq_op = PUSH;
        rq_data = NULL_EVENT;

        eq_out = NULL_EVENT;
        rq_out = NULL_EVENT;

        // File specific: init outputs
        S_to_Source = NULL_EVENT;
        S_to_A = NULL_EVENT;
        S_to_B = NULL_EVENT;

        // File specific: init finalization table
        finalized = { false, false, false };
        f_counter = 5;
        final_to_Source = finalized;
        final_to_A = finalized;
        final_to_B = finalized;
    }

    /**
     * The next() function facilitates the transition of
     * the system composed by reactors. It does two things specifically.
     *
     * 1. Check if there are input events. If so, push them into
     *    appropriate queues (might take multiple steps).
     * 2. If there are no more input events from reactors, pop from 
     *    reaction queue first. When the reaction queue becomes empty, 
     *    pop from event queue and advance time.
     */
    next {
        // Push an incoming event
        if (has_available_event()) {
            
            // File specific: select an available event to push
            case
                (is_present(Source_to_S)) : {
                    call (eq_op', eq_data', rq_op', rq_data')
                        = push_event(Source_to_S);
                }
                (is_present(A_to_S)) : {
                    call (eq_op', eq_data', rq_op', rq_data')
                        = push_event(A_to_S);
                }
                (is_present(B_to_S)) : {
                    call (eq_op', eq_data', rq_op', rq_data')
                        = push_event(B_to_S);
                }
            esac
        }
        // Pop an event from the queues
        else {
            // At this point there are no more events 
            // from both reactors, pop from queue

            // Pop from reaction queue if it is not empty.
            if (reaction_q.count > 0) {
                rq_op' = POP;
                rq_data' = NULL_EVENT;

                eq_op' = PUSH; // Pushing a NULL_EVENT is similar to NoOp.
                eq_data' = NULL_EVENT;
            }
            else {
                rq_op' = PUSH;
                rq_data' = NULL_EVENT;

                eq_op' = POP;
                eq_data' = NULL_EVENT;
            }
        }

        // File specific: distribute events to appropriate reactors
        S_to_Source' = get_event_dest(
                                get_event_present(rq_out', eq_out'),
                                Source
                               );
        S_to_A' = get_event_dest(
                                get_event_present(rq_out', eq_out'),
                                A
                               );
        S_to_B' = get_event_dest(
                                get_event_present(rq_out', eq_out'),
                                B
                               );

        // Update finalized table based on a counter.
        if (f_counter == 0) {
            call () = update_finalized_table();
            f_counter' = 5;
        }
        else {
            f_counter' = f_counter - 1;
        }

        final_to_Source' = finalized';
        final_to_A' = finalized';
        final_to_B' = finalized';

        // Update clock
        if (is_present(eq_out')) {
            t' = eq_out'._1;
        }

        // Transition queues
        next(event_q);
        next(reaction_q);
    }

}
